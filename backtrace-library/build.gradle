plugins {
    id 'com.gladed.androidgitversion' version '0.4.14'
    id "com.vanniktech.maven.publish" version "0.29.0" apply true
}
apply plugin: 'com.android.library'

import com.vanniktech.maven.publish.SonatypeHost

androidGitVersion {
    // this is the format for generating the versionName
    // default is %tag%%-count%%-commit%%-branch%%-dirty% - removed %-dirty% because submodules tend to get dirty
    format '%tag%%-count%%-commit%%-branch%'
}

android {
    namespace "backtraceio.library"
    defaultConfig {
        minSdkVersion 21
        compileSdk 36
        targetSdkVersion 36
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()
        buildConfigField("String", "VERSION_NAME", "\"${versionName}\"")

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DCMAKE_BUILD_TYPE=Release"
                arguments "-DCRASHPAD_HTTPS_TRANSPORT=OPENSSL"
            }
        }
        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
        buildFeatures {
            buildConfig = true
        }
    }

    buildTypes {
        buildTypes {
            debug {
                testCoverageEnabled false
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version "3.22.1"
        }
    }
    sourceSets {
        main {
        }
        androidTest.manifest.srcFile "src/androidTest/java/backtraceio/library/AndroidManifest.xml"
    }

    // Needed until we migrate to AndroidX
    android {
        lintOptions {
            abortOnError false
        }
    }
    ndkVersion "28.1.13356709"
}

tasks.withType(Test).configureEach {
    jacoco {
        excludes = ['net/bytebuddy/**', 'org/objenesis/**']
    }
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
    signAllPublications()
    coordinates(GROUP, POM_NAME, android.defaultConfig.versionName)

    pom {
        name = POM_NAME
        description = POM_DESCRIPTION
        url = POM_URL

        licenses {
            license {
                name = POM_LICENSE_NAME
                url = POM_LICENSE_URL
                distribution = POM_LICENSE_DIST
            }
        }

        developers {
            developer {
                id = POM_DEVELOPER_ID
                name = POM_DEVELOPER_NAME
            }
            developer {
                id = POM_DEVELOPER_ID2
                name = POM_DEVELOPER_NAME2
            }
        }

        scm {
            url = POM_SCM_URL
            connection = POM_SCM_CONNECTION
            developerConnection = POM_SCM_DEV_CONNECTION
        }
    }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.squareup:tape:1.2.3'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-core:5.18.0"
    testImplementation 'org.json:json:20250517'
    testImplementation "com.google.guava:guava:33.4.8-jre"
    androidTestImplementation 'net.jodah:concurrentunit:0.4.6'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation 'org.mockito:mockito-core:5.18.0'
    androidTestImplementation "org.mockito:mockito-android:5.18.0"
}
