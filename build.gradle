// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.11.1'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'com.diffplug.spotless' version '8.0.0' apply false
}

def spotlessExcludes = [
        '.github/**',
        '.gradle/**',
        '.idea/**',
        'build/**',
        '**/build/**',
        '**/build-*/**',
        '**/.cxx/**',
        '**/generated/**',
        '**/jniLibs/**',
        '**/libbun/**',
        '**/breakpad-builds/**',
        '**/crashpad/**',
        '**/cpp/**',
]

subprojects {
    apply plugin: 'com.diffplug.spotless'
    spotless {
        java {
            target '**/*.java'
            targetExclude spotlessExcludes
            palantirJavaFormat()
            removeUnusedImports()
            trimTrailingWhitespace()
            endWithNewline()
        }
        json {
            target '**/*.json'
            targetExclude spotlessExcludes
            simple()
        }

        format 'gradle', {
            target '**/*.gradle'
            targetExclude spotlessExcludes
            leadingTabsToSpaces(4)
            trimTrailingWhitespace()
            endWithNewline()
        }


        format 'misc', {
            target '*.md', '.gitignore'
            targetExclude spotlessExcludes
            trimTrailingWhitespace()
            endWithNewline()
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
